defmodule FountainWeb.InferenceCredentialSave do
  @moduledoc """
  Validate-then-persist an inference credential from a LiveView, with the
  messages the user sees. One place for the three doors that collect keys
  (onboarding, the credentials page, the agent form's just-in-time prompt),
  so every one validates against the provider first and audits the save
  with the socket's attribution (#546).
  """

  alias Fountain.Crypto
  alias Fountain.InferenceCredentials
  alias Fountain.InferenceCredentials.Validator

  @type outcome :: {:ok, String.t()} | {:error, String.t()}

  @doc "Validate `value` against `provider`, persist it for the socket's user, and say what happened."
  @spec save(Phoenix.LiveView.Socket.t(), atom(), String.t() | nil) :: outcome
  def save(socket, provider, value) do
    value = String.trim(value || "")

    if value == "" do
      {:error, "Paste a value before saving."}
    else
      case Validator.validate(provider, value) do
        :ok ->
          case persist(socket, provider, value) do
            {:ok, _} -> {:ok, "Saved and validated."}
            {:error, reason} -> {:error, "Could not save: #{inspect(reason)}"}
          end

        {:error, :invalid, %{status: status}} ->
          {:error,
           "Provider rejected the credential (HTTP #{status}). Check that you copied the full token."}

        {:error, :timeout} ->
          {:error, "Validation timed out. Try again."}

        {:error, reason} ->
          {:error, "Could not reach provider (#{inspect(reason)})."}
      end
    end
  end

  defp persist(socket, provider, value) do
    user_id = socket.assigns.user_id

    with {:ok, dek} <- Crypto.load_tenant_key(user_id) do
      InferenceCredentials.put_credential(
        user_id,
        dek,
        provider,
        value,
        FountainWeb.Audited.attribution(socket)
      )
    end
  end

  @doc "Human names for the providers and credentials the forms talk about."
  @spec label(atom() | String.t()) :: String.t()
  def label(:anthropic_api_key), do: "Anthropic API key"
  def label(:claude_code_oauth_token), do: "Claude OAuth token"
  def label(:openai_api_key), do: "OpenAI API key"
  def label(:gemini_api_key), do: "Gemini API key"
  def label("anthropic"), do: "Anthropic"
  def label("openai"), do: "OpenAI"
  def label("google"), do: "Google"
  def label(other), do: to_string(other)

  @doc "Where to get one."
  @spec source(atom()) :: String.t()
  def source(:anthropic_api_key), do: "console.anthropic.com"
  def source(:claude_code_oauth_token), do: "`claude setup-token`"
  def source(:openai_api_key), do: "platform.openai.com/api-keys"
  def source(:gemini_api_key), do: "aistudio.google.com/apikey"
  def source(_), do: ""
end
